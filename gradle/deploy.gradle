/*
 * Copyright (C) 2014 Andrew Reitz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * Limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

def releaseBuild = false
def devBuild = false
def sonaTypeUrl = ''

if (hasProperty("release")) {
  releaseBuild = true
  sonaTypeUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("snapshot")) {
  VERSION += "-SNAPSHOT"
  sonaTypeUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
  devBuild = true
}

afterEvaluate { project ->
  uploadArchives {
    repositories {
      if (devBuild) {
        mavenLocal()
      } else {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: sonaTypeUrl) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
          }

          pom.project {
            name PROJECT_NAME
            packaging 'jar'
            description 'Better Preferences for Android'
            artifactId = ARTIFACT_ID
            groupId GROUP
            version VERSION
            url 'https://github.com/InkApplications/android-preferences'

            scm {
              url 'scm:https://github.com/InkApplications/android-preferences'
              connection 'scm:git@github.com:InkApplications/android-preferences.git'
              developerConnection 'scm:git@github.com:InkApplications/android-preferences.git'
            }

            licenses {
              license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
              }
            }

            developers {
              developer {
                id 'areitz'
                name 'Andrew Reitz'
                url "http://andrewreitz.com"
              }
            }
          }
        }
      }
    }
  }

  signing {
    required { releaseBuild && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
  }

  task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  }

  task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
  }

  task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
  }

  artifacts {
    archives androidSourcesJar, androidJavadocsJar
  }
}
